vim9script

import autoload "highlighter.vim"
import autoload "comment.vim"
import autoload "html_tags.vim"
import autoload "snippet.vim"

g:loaded_getscriptPlugin = 1
g:loaded_gzip = 1
g:loaded_logiPat = 1
g:loaded_manpager_plugin = 1
g:loaded_matchparen = 1
g:loaded_netrw = 1
g:loaded_rrhelper = 1
g:loaded_spellfile_plugin = 1
g:loaded_tarPlugin = 1
g:loaded_2html_plugin = 1
g:loaded_tutor_mode_plugin = 1
g:loaded_vimballPlugin = 1
g:loaded_zipPlugin = 1

$BASH_ENV = "~/.bash_aliases"
g:mapleader = " "

set nocompatible

set ruler
set showcmd
set ttimeout
set ttimeoutlen=50
set display=lastline
set incsearch
set hlsearch
set nrformats=bin,hex
set mouse=a
set nolangremap
set scrolloff=20
set tabstop=4
set shiftwidth=4
set expandtab
set smarttab
set autoindent
set smartindent
set nowrap
set signcolumn=yes
set colorcolumn=80
set list
set listchars=tab:»\ ,trail:·
set splitbelow
set splitright
set viminfo=
set hidden
set laststatus=2
set wildoptions=pum,tagfile
set shortmess=ltToOCF
set clipboard=autoselectplus,exclude:cons\|linux
set belloff=all
set autoread
set complete=.,w,b,t
set fillchars=vert:│
set formatoptions=croqj
set nojoinspaces
set sidescroll=1
set nostartofline
set switchbuf=useopen
set background=dark

augroup show_listchars
    au!
    au InsertEnter * set nolist
    au InsertLeave * set list
augroup END

augroup highlight_shebang
    au!
    au FileType * {
        syn match global_shebang "\%^#!.*"
        hi default link global_shebang Comment
    }
augroup END

colorscheme art
filetype plugin on
syntax on

command -nargs=* -complete=shellcmdline Term term ++shell <args>
command Todo sp $HOME/.local/share/todo/list
command SnippetListAvailable snippet.ListAvailable()

nnoremap <C-L> <cmd>nohlsearch<cr>
nnoremap dl 0D

nnoremap <leader>hs <scriptcmd>highlighter.Strings()<cr>

nnoremap <leader>/ <scriptcmd>comment.Toggle()<cr>
vnoremap <leader>/ <scriptcmd>comment.Toggle()<cr><esc>

############### art, 27.03.25: write a proper plugin
nnoremap <leader>ff <cmd>Term fd -t f -E .git -E node_modules<cr>
nnoremap <expr> <leader>fc MapFindCurrent()
nnoremap <expr> <leader>fs MapFindString()
nnoremap <expr> <leader>fg MapFindGlobal()

vnoremap <expr> <leader>fs MapFindString('v')

def MapFindCurrent(): string
    var val = input("Buffer Grep> ")
    if trim(val) == ""
        return "\<esc>"
    endif
    return $"\<cmd>Term rg -i -- \"{val}\" %\<cr>"
enddef

def MapFindString(mode = "n"): string
    var word = expand("<cword>")

    if mode == "v"
        var s = charcol("v")
        var e = charcol(".")

        if e < s
            var tmp = s

            s = e
            e = tmp
        endif
        word = slice(getline("."), s - 1, e)
    endif

    # " ' < > \t \n < > ( ) [ ] { } ? * + | $ . ^ ` \
    word = escape(word, '\$#')
    word = escape(word, '()[]{}?*+|$^.#`\"')
    return $"\<cmd>Term rg -i -w -g=!.git -g=!node_modules -- \"{word}\"\<cr>"
enddef

def MapFindGlobal(): string
    var val = input("PWD Grep> ")
    if trim(val) == ""
        return "\<esc>"
    endif
    return $"\<cmd>Term rg -i -g=!.git -g=!node_modules -- \"{val}\"\<cr>"
enddef
###############

inoremap <expr> <tab> HandleTabInsertMode()

def HandleTabInsertMode(): string
    if html_tags.LineHasTrigger()
        return "\<scriptcmd>html_tags.CompileLine()\<cr>"
    endif
    if snippet.LineHasTrigger()
        return "\<scriptcmd>snippet.Expand()\<cr>"
    endif
    return "\<tab>"
enddef
